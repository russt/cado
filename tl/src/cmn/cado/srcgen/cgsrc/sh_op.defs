#define some specialized ops for operating on shell scripts.
{
SH_OP_STUBS := << EOF_OPS
sub factorCshVars_op
{
    my ($var, $varname, $linecnt) = @_;
    my $prefix = "cshvar_";
    my $xpat = "";
    my $ipat = "";

    #output intermediate variables:
    my $shvardeftxt = "";
    my (@shvars) = ();
    my (@cgshvars) = ();
    my (@shvarvals) = ();

    if ( &var_defined_non_empty("CG_CSHVAR_PREFIX") ) {
        $prefix = $CG_USER_VARS{'CG_CSHVAR_PREFIX'}          
    } else {
        #otherwise, initialize prefix to the default so user can retrieve it:
        &assign_op($prefix, 'CG_CSHVAR_PREFIX', $linecnt);
    }

    $xpat =   $CG_USER_VARS{'CG_CSHVAR_EXCLUDE_PATTERN'} if ( &var_defined_non_empty("CG_CSHVAR_EXCLUDE_PATTERN") );
    $ipat =   $CG_USER_VARS{'CG_CSHVAR_INCLUDE_PATTERN'} if ( &var_defined_non_empty("CG_CSHVAR_INCLUDE_PATTERN") );

    #remove leading/trailing slashes if necessary:
    ($xpat =~ s|^/|| && $xpat =~ s|/$||) unless ($xpat eq "");
    ($ipat =~ s|^/|| && $ipat =~ s|/$||) unless ($ipat eq "");

    #clear output variables:
    &assign_op("", 'CG_CSHVAR_DEFS', $linecnt);
    &assign_op("", 'CG_CSHVAR_LIST', $linecnt);
    &assign_op("", 'CG_CSHVARVAL_LIST', $linecnt);

    #variable reference forms for csh:
    #    set foo = (xx yy zz)
    #    echo form1=$?foo
    #    echo form2=${?foo}
    #    echo form3=$#foo
    #    echo form4=${#foo}
    #    echo form5=$foo[2]
    #    echo form6=${foo[2]}
    
    #####
    #LOOP 1 - find variable initializations and save them:
    #####

    ##########
    #eliminate continuation lines, and split variable text.
    #RE patterns used below are line-by-line based, and do not handle continuation lines.
    #TODO:  handle multi-line statements.  this is currently handled poorly.
    ##########
    $var =~ s/\\\n//sg;
    my (@var) = split("\n", $var, -1);

    my $line = "";
    foreach $line (@var) {
        #skip comment, lines without var defs:
        next if ($line =~ /^\s*#/ || $line =~ /^\s*$/);

        my $set_def = 'set\s+([a-z_A-Z]\w*)\s*=';
        my $env_def = 'setenv\s+([a-z_A-Z]\w*)(\s|$)';
        my $re_ref = '\$\{?[#?]?([a-z_A-Z]\w*)';

        next unless ($line =~ /$set_def/ || $line =~ /$env_def/ || $line =~ /$re_ref/);
        my $shvname = $1;

        #ignore if we are excluding this variable name:
        next if ( &sh_name_is_excluded($shvname, $ipat, $xpat) );

        #otherwise, we have a var - add to list:
        push @shvars, $shvname;
    }

    #eliminate duplicates, preserving order:
    my %VARVALS = ();
    my (@tmp) = ();
    for (@shvars) {
        next if (defined($VARVALS{$_}));
        $VARVALS{$_} = 0;   #later used to generate macro names for values assigned to this var.
        push @tmp, $_;
    }
    @shvars = @tmp;

    #create output array for CG_CSHVAR_LIST
    @cgshvars = @shvars;
    grep($_ =~ s/^/${prefix}/, @cgshvars);

#printf STDERR "cgshvars=(%s)\n", join(",", @cgshvars);

    #####
    #LOOP 2 - foreach var def, substitute in macro name:
    #####

    my $shvar = "";
    #sort backwards so longest variable names are applied first:
    foreach $shvar (sort { $b cmp $a } @shvars) {
        my $cg_shvar = "$prefix$shvar";
        my $macroref = "{##$cg_shvar##}";    #use temp delimiters for macro brackets

        my $re_def = '(set\s+)' . $shvar . '(\s*=)';
        my $re_ref = '(\$\{?[#?]?)' . $shvar;
        my $re_iden = '(\s|#)' . $shvar . '($|\s|[\-\.,;&|])';

        #do substitutions for defs & refs:
        grep($_ =~ s/$re_def/$1${macroref}$2/g, @var);
        grep($_ =~ s/$re_ref/$1${macroref}/g, @var);
        grep(/^\s*(setenv\s|unsetenv\s|unset\s|#)/ && $_ =~ s/$re_iden/$1${macroref}$2/g, @var);

        #generate variable definition:
        $shvardeftxt .= sprintf("\n%s := %s\n", $cg_shvar, $shvar);

        #for each definitional instance, create value variables:
        my ($varvaltxt, $lref, $cg_varval, $varval_macro, $issetenv);
        for (@var) {
            $lref = \$_;
            if ($$lref =~ /setenv[ \t]+(${macroref})[ \t]+(.*)$/) {
                $varvaltxt = $2;
                $issetenv = 1;
            } elsif ($$lref =~ /set[ \t]+(${macroref})[ \t]*=[ \t]*(.*)$/) {
                $varvaltxt = $2;
                $issetenv = 0;
            } else {
                #line does not contain set or setenv pattern:
                next;
            }

            $varvaltxt  =~ s/{##/{=/g;
            $varvaltxt  =~ s/##}/=}/g;
#printf "line='%s' 1='%s' 2='%s' varvaltxt='%s'\n", $$lref, $1, $2, $varvaltxt;

            $cg_varval = sprintf("%s_val%02d", $cg_shvar, ++$VARVALS{$shvar});
            push(@shvarvals, $cg_varval);
            $varval_macro = "{##$cg_varval##}";

            #this only does first substitution - does not work on multi-statement lines
            #or assignments with continuation lines:
            if ($issetenv) {
                $$lref =~ s/(setenv[ \t]+${macroref}[ \t]+)(.*)$/$1${varval_macro}/;
            } else {
                $$lref =~ s/(set[ \t]+${macroref}[ \t]*=[ \t]*)(.*)$/$1${varval_macro}/;
            }

            #add var-value definition:
            $shvardeftxt .= sprintf("%s := %s\n", $cg_varval, $varvaltxt);
        }
    }

    #substitute in correct macro brackets:
    grep($_ =~ s/{##/{=/g, @var);
    grep($_ =~ s/##}/=}/g, @var);

    ######
    #write variable text instrumented with macros:
    ######
    $var = join("\n", @var);

    ######
    #write results to user vars:
    ######
    my $FS = &lookup_def('CG_STACK_DELIMITER');

    #overwrite results if we had any:
    &assign_op($shvardeftxt, 'CG_CSHVAR_DEFS', $linecnt)          if ($shvardeftxt ne "");;
    &assign_op(join($FS, @cgshvars), 'CG_CSHVAR_LIST', $linecnt)     if ($#cgshvars >= 0);
    &assign_op(join($FS, @shvarvals), 'CG_CSHVARVAL_LIST', $linecnt) if ($#shvarvals >= 0);

    return $var;
}

sub factorShVars_op
{
    my ($var, $varname, $linecnt) = @_;
    my $prefix = "shvar_";
    my $xpat = "";
    my $ipat = "";

    #output intermediate variables:
    my $shvardeftxt = "";
    my (@shvars) = ();
    my (@cgshvars) = ();
    my (@shvarvals) = ();

    if ( &var_defined_non_empty("CG_SHVAR_PREFIX") ) {
        $prefix = $CG_USER_VARS{'CG_SHVAR_PREFIX'}          
    } else {
        #otherwise, initialize prefix to the default so user can retrieve it:
        &assign_op($prefix, 'CG_SHVAR_PREFIX', $linecnt);
    }

    $xpat =   $CG_USER_VARS{'CG_SHVAR_EXCLUDE_PATTERN'} if ( &var_defined_non_empty("CG_SHVAR_EXCLUDE_PATTERN") );
    $ipat =   $CG_USER_VARS{'CG_SHVAR_INCLUDE_PATTERN'} if ( &var_defined_non_empty("CG_SHVAR_INCLUDE_PATTERN") );

    #remove leading/trailing slashes if necessary:
    ($xpat =~ s|^/|| && $xpat =~ s|/$||) unless ($xpat eq "");
    ($ipat =~ s|^/|| && $ipat =~ s|/$||) unless ($ipat eq "");

    #clear output variables:
    &assign_op("", 'CG_SHVAR_DEFS', $linecnt);
    &assign_op("", 'CG_SHVAR_LIST', $linecnt);
    &assign_op("", 'CG_SHVARVAL_LIST', $linecnt);

    #
    #Q:  what about when an equals appears in a string?  i.e., "foo=$foo" or 'foo=$foo'
    #Q:  what an equals appears in an echo or printf statement?
    #Q:  what if the equals is backslash escaped to the next line?  i.e.:
    #    foo\
    #    =xxx
    #

    #####
    #LOOP 1 - find variable initializations and save them:
    #####

    ##########
    #eliminate continuation lines, and split variable text.
    #RE patterns used below are line-by-line based, and do not handle continuation lines.
    #TODO:  handle multi-line statements.  this is currently handled poorly.
    ##########
    $var =~ s/\\\n//sg;
    my (@var) = split("\n", $var, -1);

    my $line = "";
    foreach $line (@var) {
        #skip comment, lines without var defs:
        next if ($line =~ /^\s*#/ || $line =~ /^\s*$/);

        #regular expressions defining sh variable defs and refs:
        my $re_def = '([a-z_A-Z]\w*)=';
        my $re_ref = '\$\{?([a-z_A-Z]\w*)';

        next unless ($line =~ /$re_def/ || $line =~ /$re_ref/);
        my $shvname = $1;

        #ignore if we are excluding this variable name:
        next if ( &sh_name_is_excluded($shvname, $ipat, $xpat) );

        #otherwise, we have a var - add to list:
        push @shvars, $shvname;
    }

    #eliminate duplicates, preserving order:
    my %VARVALS = ();
    my (@tmp) = ();
    for (@shvars) {
        next if (defined($VARVALS{$_}));
        $VARVALS{$_} = 0;   #later used to generate macro names for values assigned to this var.
        push @tmp, $_;
    }
    @shvars = @tmp;

    #create output array for CG_SHVAR_LIST
    @cgshvars = @shvars;
    grep($_ =~ s/^/${prefix}/, @cgshvars);

#printf STDERR "cgshvars=(%s)\n", join(",", @cgshvars);

    #####
    #LOOP 2 - foreach var def, substitute in macro name:
    #####

    my $shvar = "";
    #sort backwards so longest variable names are applied first:
    foreach $shvar (sort { $b cmp $a } @shvars) {
        my $cg_shvar = "$prefix$shvar";
        my $macroref = "{##$cg_shvar##}";    #use temp delimiters for macro brackets

        my $re_def = '(^|\s|[;&|])' . $shvar . '(=)';
        my $re_ref = '(\$\{?)' . $shvar;
        my $re_iden = '(\s|#)' . $shvar . '($|\s|[\-\.,;&|])';

        #do substitutions for defs & refs:
        grep($_ =~ s/$re_def/$1${macroref}$2/g, @var);
        grep($_ =~ s/$re_ref/$1${macroref}/g, @var);
        grep(/^\s*(export\s|unset\s|#)/ && $_ =~ s/$re_iden/$1${macroref}$2/g, @var);

        #generate variable definition:
        $shvardeftxt .= sprintf("\n%s := %s\n", $cg_shvar, $shvar);

        #for each definitional instance, create value variables:
        my ($varvaltxt, $lref, $cg_varval, $varval_macro);
        for (@var) {
            $lref = \$_;
            next unless ($$lref =~ /(${macroref})=(.*)$/);
            $varvaltxt = $2;
            $varvaltxt  =~ s/{##/{=/g;
            $varvaltxt  =~ s/##}/=}/g;
#printf "line='%s' 1='%s' 2='%s' varvaltxt='%s'\n", $$lref, $1, $2, $varvaltxt;

            $cg_varval = sprintf("%s_val%02d", $cg_shvar, ++$VARVALS{$shvar});
            push(@shvarvals, $cg_varval);
            $varval_macro = "{##$cg_varval##}";

            #this only does first substitution - does not work on multi-statement lines
            #or assignments with continuation lines:
            $$lref =~ s/(${macroref})=(.*$)/$1=${varval_macro}/;

            #add var-value definition:
            $shvardeftxt .= sprintf("%s := %s\n", $cg_varval, $varvaltxt);
        }
    }

    #substitute in correct macro brackets:
    grep($_ =~ s/{##/{=/g, @var);
    grep($_ =~ s/##}/=}/g, @var);

    ######
    #write variable text instrumented with macros:
    ######
    $var = join("\n", @var);

    ######
    #write results to user vars:
    ######
    my $FS = &lookup_def('CG_STACK_DELIMITER');

    #overwrite results if we had any:
    &assign_op($shvardeftxt, 'CG_SHVAR_DEFS', $linecnt)          if ($shvardeftxt ne "");;
    &assign_op(join($FS, @cgshvars), 'CG_SHVAR_LIST', $linecnt)     if ($#cgshvars >= 0);
    &assign_op(join($FS, @shvarvals), 'CG_SHVARVAL_LIST', $linecnt) if ($#shvarvals >= 0);

    return $var;
}

sub factorShSubs_op
{
    my ($var, $varname, $linecnt) = @_;
    my $prefix = "shsub_";
    my $xpat = "";
    my $ipat = "";

    if ( &var_defined_non_empty("CG_SHSUB_PREFIX") ) {
        $prefix = $CG_USER_VARS{'CG_SHSUB_PREFIX'}          
    } else {
        #otherwise, initialize prefix to the default so user can retrieve it:
        &assign_op($prefix, 'CG_SHSUB_PREFIX', $linecnt);
    }

    $xpat =   $CG_USER_VARS{'CG_SHSUB_EXCLUDE_PATTERN'} if ( &var_defined_non_empty("CG_SHSUB_EXCLUDE_PATTERN") );
    $ipat =   $CG_USER_VARS{'CG_SHSUB_INCLUDE_PATTERN'} if ( &var_defined_non_empty("CG_SHSUB_INCLUDE_PATTERN") );

    #remove leading/trailing slashes if necessary:
    ($xpat =~ s|^/|| && $xpat =~ s|/$||) unless ($xpat eq "");
    ($ipat =~ s|^/|| && $ipat =~ s|/$||) unless ($ipat eq "");

    #clear output variables:
    &assign_op("", 'CG_SHSUB_DEFS', $linecnt);
    &assign_op("", 'CG_SHSUB_LIST', $linecnt);

    #INPUT:
    # a()
    # {
    #     echo sub a: $v_1
    # }
    # 
    # b (  ){
    #     echo sub b: $v2
    #     }
    # 
    # # c() { echo sub c }
    # 
    # c()
    # {
    # echo sub c: $V3
    # }
    #
    # _123()
    # {
    # echo sub _123
    # a=${v2}
    # }
    #
    #OUTPUT:
    # {=shsub_a=}
    # {=shsub_b=}
    # #c() { echo sub c }
    # {=shsub_c=}
    # {=shsub__123=}
    #
    #NOTES:
    # - brackets are only used in var refs & subroutine defs.
    # - expression below requires at least one newline before each subrouting declaration.
    #   this means that we will miss subroutines declared in first line of file.
    # - note the use of .+? in the expression.  this forces the engine to match the first
    #{  instance of \n\s*} which terminates the subroutine def.  otherwise, it would do
    #   a "greedy" match, and match the last instance.
    # - the /so modifiers treat the multi-line string as a single string,
    #   and compile the pattern only once.

    my %shsub_defs = ();
    my %shsub_names = ();
    my @cg_srnames_initial = ();
    my $re = '\n([\t ]*)([a-z_A-Z]\w*)(\s*\(\s*\)[^{]*\{.+?\n\s*\})(\s*?)\n';
    #} match bracket
    my ($cg_srname, $srtxt, $srname, $wsp_leading, $wsp_trailing) = ("", "", "", "", "");

    #####
    #LOOP 1 - parse subroutines declarations and save them:
    #####

    while ($var =~ /$re/so ) {
        $wsp_leading = $1;
        $srname = $2;
        $srtxt = "$2$3";
        $wsp_trailing = $4;
        $cg_srname = "$prefix$srname";

#printf "srtxt='%s'\n", $srtxt;

        my $repl = sprintf("\n%s{=%s=}%s\n", $wsp_leading, $cg_srname, $wsp_trailing);

        #replace the subroutine text with the generated cg macro name:
        $var =~ s/$re/$repl/so;

        #save the name and text of the subroutine:
        $shsub_defs{$cg_srname} = $srtxt;
        $shsub_names{$cg_srname} = $srname;

        #push the cg sr name on to preserve order of input:
        push @cg_srnames_initial, $cg_srname;
    }

#printf "INTERMEDIATE VAR=,%s,\n", $var;

    my (@srnames) = ();
    my (@cg_srnames_final) = ();

    #####
    #LOOP 2  - restore the original text for subroutines we are ignoring.
    #####

    #####
    foreach $cg_srname (@cg_srnames_initial) {
        $srtxt = $shsub_defs{$cg_srname};
        $srname = $shsub_names{$cg_srname};
        my $macroref = "{=$cg_srname=}";

        #if this subroutine is excluded by name...
        if ( &sh_name_is_excluded($srname, $ipat, $xpat) ) {
            #... then restore original text in the input:
            $var =~ s/$macroref/$srtxt/s;

            next;   #do not output text or variable if we are excluding
        }

        #otherwise, append to user variables:  definition text, and subroutine name list:
        push @srnames, $srname;
        push @cg_srnames_final, $cg_srname;
    }

    #####
    #LOOP 3 - replace references to factored subroutines with macros:
    #####

    #split variable text:
    my (@var) = split("\n", $var, -1);

    #sort backwards so longest variable names are applied first:
    foreach $cg_srname (sort { $b cmp $a } @cg_srnames_final) {
        $srname = $shsub_names{$cg_srname};
        my $cg_srname_ref = "${cg_srname}_ref";
        my $macroref = "{=$cg_srname_ref=}";

        #note option to match at ^ or $.
        my $re   = '(^|[;:|&\s])' . $srname . '($|[;:|&\s])' ;

        grep( $_ !~ /^\s*#/ && s/$re/$1${macroref}$2/g, @var);

        #also perform substitutions for text of each subroutine:
        my $cg_srname2 = "";
        foreach $cg_srname2 (@cg_srnames_final) {
            my (@srtxt) = split("\n", $shsub_defs{$cg_srname2}, -1);
            if (grep( $_ !~ /^\s*#/ && s/$re/$1${macroref}$2/g, @srtxt)) {
                $shsub_defs{$cg_srname2} = join("\n", @srtxt);
            }
        }
    }

    #restore variable text:
    $var = join("\n", @var);

    #definiton PREFIX:
    #set pragma to trim final newlines in here-now defs we generate for subroutines.
    #this allows us to substitute macros and restore the spacing of the original text.
    my $srdeftxt = << "!";
_save_trim_multiline_rnewline = \$trim_multiline_rnewline:pragmavalue
\%pragma trim_multiline_rnewline 1

!

    #####
    #LOOP 4 - write out definitions:
    #####

    foreach $cg_srname (@cg_srnames_final) {
        $srname = $shsub_names{$cg_srname};
        my $cg_srname_ref = "${cg_srname}_ref";
        $srtxt = $shsub_defs{$cg_srname};

        #output definition:
        $srdeftxt .= << "!";

##sh subroutine $srname()
${cg_srname}_ref := $srname
$cg_srname := << ${prefix}EOF
$srtxt
${prefix}EOF

!
    }

    #POST-SCRIPT:
    $srdeftxt .= << "!";

#restore normal behavior for here-now defs:
\%pragma trim_multiline_rnewline = \$_save_trim_multiline_rnewline

!


    ######
    #write results to user vars:
    ######
    my $FS = &lookup_def('CG_STACK_DELIMITER');

    #overwrite results if we had any:
    &assign_op($srdeftxt, 'CG_SHSUB_DEFS', $linecnt)          if ($srdeftxt ne "");;
    &assign_op(join($FS, @srnames), 'CG_SHSUB_LIST', $linecnt) if ($#srnames >= 0);

    return $var;
}

sub sh_name_is_excluded
#return 1 if <name> is excluded, 0 otherwise
#(local  utility).
{
    my ($name, $ipat, $xpat) = @_;

    #do not excluded if neither include or exclude pattern was specified:
    return 0 if ($ipat eq "" && $xpat eq "");

    #exclude if exclude pattern is specified and matches:
    return 1 if ($xpat ne "" && $name =~ /$xpat/);

    #do not exclude if include pattern was not specified or if it is specified and matches:
    return 0 if ($ipat eq "" || $name =~ /$ipat/);

    #include pattern was specified, but did not match:
    return 1;
}

EOF_OPS
}
